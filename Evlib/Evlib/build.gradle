plugins {
    id 'com.android.library'
    id 'com.google.devtools.ksp' version '1.6.21-1.0.5'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
    id 'idea'
    id 'com.github.willir.rust.cargo-ndk-android'

    id 'org.jetbrains.dokka'
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
        versionCode 0
        versionName '0.1.0'
    }

    buildTypes {
        release {
            minifyEnabled true
        }
    }

    lintOptions {
        tasks.lint.enabled false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

idea {
    module {
        sourceDirs = sourceDirs + file('build/generated/ksp/debug/kotlin')
    }
}

//File generatedSourceDir = new File(buildDir, 'generated/ksp/debug/kotlin')
tasks.named("dokkaHtml") {
    outputDirectory.set(file('build/dokka'))
    dokkaSourceSets.configureEach {
        sourceRoots.from(file('build/generated/ksp/debug'))
        noAndroidSdkLink.set(false)
        suppressGeneratedFiles.set(false)
    }
}

cargoNdk {
    // REV Robotics hardware runs on arm, but TODO need to find whether 32 or 64 bit
    targets = ['arm64', 'arm']
    module = 'Evlib/Evlib/src/native'
    extraCargoBuildArguments = ['-Zbuild-std']
}

dependencies {
    implementation 'org.firstinspires.ftc:RobotCore:7.1.0'
    implementation 'org.firstinspires.ftc:Hardware:7.1.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    api project(':Evlib:EvAnnotations')
    ksp project(':Evlib:EvSymbolProcessor')
}